plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

loom {
    accessWidenerPath = file("src/main/resources/evaware.accesswidener")
}

repositories {
    mavenCentral()
    maven {
        name = "FirstDark"
        url 'https://maven.firstdark.dev/releases'
    }
    maven {
        name = "Jitpack"
        url = uri("https://jitpack.io")
    }
    maven {
        name = 'Ladysnake Mods'
        url = 'https://maven.ladysnake.org/releases'
        content {
            includeGroup 'io.github.ladysnake'
            includeGroup 'org.ladysnake'
            includeGroupByRegex 'dev\\.onyxstudios.*'
        }
    }
}

configurations {
    libImplementation
}

dependencies {
    // minecraft and fabric
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // lombok
    compileOnly "org.projectlombok:lombok:${project.lombok_version}"
    annotationProcessor "org.projectlombok:lombok:${project.lombok_version}"

    // ai
    libImplementation "ai.catboost:catboost-common:${project.ai_version}"
    libImplementation "ai.catboost:catboost-prediction:${project.ai_version}"

    // discord
    libImplementation "com.github.donyka:discord-rpc:${project.drpc_version}"

    // satin
    modImplementation "org.ladysnake:satin:${satin_version}"

    // json
    modImplementation "org.json:json:${project.json_version}"

    configurations.libImplementation.dependencies.each {
        implementation(it)
    }
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()

}

jar {
    inputs.property "archivesName", project.base.archivesName

    from {
        configurations.libImplementation.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)

    from("LICENSE") {
        rename { "${it}_${inputs.properties.archivesName}"}
    }
}

publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}
}